plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
    id 'kotlin-kapt'
    id 'kotlin-android'
    id 'kotlinx-serialization'
}

apply from: secret

android {
    namespace 'com.skyd.raca'
    compileSdk 33

    defaultConfig {
        applicationId "com.skyd.raca"
        minSdk 24
        targetSdk 33
        versionCode 7
        versionName "1.6"
        flavorDimensions "versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    signingConfigs {
        release {
            keyAlias secret.sign.RELEASE_KEY_ALIAS
            keyPassword secret.sign.RELEASE_KEY_PASSWORD
            storeFile file('../key.jks')
            storePassword secret.sign.RELEASE_STORE_PASSWORD
        }
    }

    productFlavors {
        GitHub
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "app_release_${variant.productFlavors[0].name.toLowerCase()}_${variant.versionName}.apk"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false           // 使用keep.xml，keep住某些资源文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'    // 一台手机debug release共存
            ndk {
                abiFilters 'armeabi', 'x86', 'x86_64', 'arm64-v8a'
            }
        }
        release {
            signingConfig signingConfigs.release    //签名
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true       // 使用keep.xml，keep住某些资源文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters 'armeabi', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'DebugProbesKt.bin'
            excludes += 'kotlin-tooling-metadata.json'
            excludes += 'okhttp3/internal/publicsuffix/NOTICE'
            excludes += 'XPP3_1.1.3.3_VERSION'
            excludes += 'XPP3_1.1.3.2_VERSION'
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
        freeCompilerArgs += "-opt-in=androidx.compose.material.ExperimentalMaterialApi"
        freeCompilerArgs += "-opt-in=androidx.compose.animation.ExperimentalAnimationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi"
        freeCompilerArgs += "-opt-in=coil.annotation.ExperimentalCoilApi"
        freeCompilerArgs += "-opt-in=androidx.compose.material3.windowsizeclass.ExperimentalMaterial3WindowSizeClassApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.layout.ExperimentalLayoutApi"
        freeCompilerArgs += "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-opt-in=kotlinx.serialization.ExperimentalSerializationApi"
    }
}

kapt {
    correctErrorTypes true
}

dependencies {

    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:$md3_version"
    implementation "androidx.compose.material3:material3-window-size-class:$md3_version"
    implementation "androidx.compose.material:material-icons-extended:1.4.0"
    implementation "androidx.compose.ui:ui-tooling-preview:$md3_version"
    implementation "com.google.android.material:material:1.9.0-beta01"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.activity:activity-compose:1.7.0"
    implementation "com.google.dagger:hilt-android:2.45"
    kapt "com.google.dagger:hilt-android-compiler:2.45"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.security:security-crypto:1.1.0-alpha05"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "io.coil-kt:coil-compose:2.1.0"
    implementation "androidx.profileinstaller:profileinstaller:1.3.0"
    implementation "androidx.core:core-splashscreen:1.0.0"
    implementation "androidx.room:room-runtime:2.5.1"
    implementation "androidx.room:room-ktx:2.5.1"
    ksp "androidx.room:room-compiler:2.5.1"
    implementation "com.github.doyaaaaaken:kotlin-csv-jvm:1.8.0"
    implementation "com.github.thegrizzlylabs:sardine-android:0.8"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0"
    implementation "com.github.Kyant0:Monet:0.1.0-alpha03"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "com.github.stuxuhai:jpinyin:1.1.8"
    implementation "com.airbnb.android:lottie-compose:6.0.0"

    debugImplementation "androidx.compose.ui:ui-tooling:$md3_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$md3_version"
}